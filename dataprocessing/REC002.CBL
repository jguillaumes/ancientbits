IDENTIFICATION DIVISION.
PROGRAM-ID. REC002.

ENVIRONMENT DIVISION.
CONFIGURATION SECTION.
SOURCE-COMPUTER. DECSYSTEM-20.
OBJECT-COMPUTER. DECSYSTEM-20.
SPECIAL-NAMES.
	DECIMAL-POINT IS COMMA.

INPUT-OUTPUT SECTION.
FILE-CONTROL.
    SELECT RECA02 ASSIGN TO DSK
           ORGANIZATION IS SEQUENTIAL
           ACCESS IS SEQUENTIAL.

DATA DIVISION.
FILE SECTION.
FD  RECA02 LABEL RECORDS ARE STANDARD
           DATA RECORDS ARE TRANS
           VALUE OF ID IS 'RECA02DAT'
           RECORDING MODE IS ASCII.
    COPY TRANS.

SCHEMA SECTION.
    INVOKE SUB-SCHEMA RECUPD 
        OF SCHEMA RECRDS.

WORKING-STORAGE SECTION.
01  EOF-SW                   PIC X VALUE SPACE.
    88 EOF VALUE 'S'.

01  EOS-SW                   PIC X VALUE SPACE.
    88 EOS VALUE 'S'.

01  TRACK-OK-SW              PIC X VALUE SPACE.
    88 TRACK-OK VALUE 'S'.

01  LP-OK-SW                 PIC X VALUE SPACE.
    88 LP-OK VALUE 'S'.

01  COMPT.
    03 LP-PROC               PIC S9(9) COMP VALUE ZERO.
    03 TRACK-PROC            PIC S9(9) COMP VALUE ZERO.
    03 IN-READ               PIC S9(9) COMP VALUE ZERO.
    03 ERRORS                PIC S9(9) COMP VALUE ZERO.
    03 IGNORED               PIC S9(9) COMP VALUE ZERO.

01  COMPT-P.
    03 LP-PROC               PIC ZZZ.ZZZ.ZZ9.
    03 TRACK-PROC            PIC ZZZ.ZZZ.ZZ9.
    03 IN-READ               PIC ZZZ.ZZZ.ZZ9.
    03 ERRORS                PIC ZZZ.ZZZ.ZZ9.
    03 IGNORED               PIC ZZZ.ZZZ.ZZ9.

01  TRACK-LINE.
    03 TLNUM                 PIC XX.
    03 FILLER                PIC X VALUE SPACE.
    03 TLNAME                PIC X(40).
    03 FILLER                PIC X.
    03 TLSECONDS             PIC ZZ.ZZ9.

77  SECONDS-P                PIC ZZ.ZZ9.

PROCEDURE DIVISION.
MAIN SECTION.
BEGIN.
    DISPLAY 'REC002: BEGIN'.
    OPEN JOURNAL USAGE-MODE UPDATE
    OPEN AREA LP-AREA
        USAGE-MODE UPDATE.
    IF ERROR-COUNT > 0
        DISPLAY ERROR-STATUS, GO TO FINISH.
    OPEN INPUT RECA02
    PERFORM DO-READ
    PERFORM DO-PROCESS UNTIL EOF
    PERFORM DO-STATS
    DISPLAY 'REC002: FINISH'
    GO TO FINISH
    .

DO-PROCESS.
    IF TCODE OF TRANS EQUAL 'CL'
        PERFORM PROCESS-CREATE-LP
    ELSE IF TCODE OF TRANS EQUAL 'DL'
        PERFORM PROCESS-DELETE-LP
    ELSE IF TCODE OF TRANS EQUAL 'UL'
        PERFORM PROCESS-UPDATE-LP
    ELSE IF TCODE OF TRANS EQUAL 'SL'
        PERFORM PROCESS-SHOW-LP
    ELSE IF TCODE OF TRANS EQUAL 'LL'
        PERFORM PROCESS-LIST-LP
    ELSE IF TCODE OF TRANS EQUAL 'CT'
        PERFORM PROCESS-CREATE-TRACK
    ELSE IF TCODE OF TRANS EQUAL 'DT'
        PERFORM PROCESS-DELETE-TRACK
    ELSE IF TCODE OF TRANS EQUAL 'UT'
        PERFORM PROCESS-UPDATE-TRACK
    ELSE IF TCODE OF TRANS EQUAL 'ST'
       PERFORM PROCESS-SHOW-TRACK
    ELSE IF TCODE OF TRANS NOT EQUAL '* ' AND NOT EQUAL '**'    
        PERFORM REPORT-ERROR.
    PERFORM DO-READ
    .

PROCESS-CREATE-LP.
    MOVE LPID     OF TRANS TO LP-ID
    MOVE LPNAME   OF TRANS TO LP-NAME
    MOVE LPARTIST OF TRANS TO LP-AUTHOR
    STORE LP-RECORD
    IF ERROR-COUNT EQUAL ZERO
        DISPLAY 'REC002: ADDED LP WITH ID ' LP-ID
        ADD 1 TO LP-PROC OF COMPT
    ELSE
        IF ERROR-STATUS EQUAL 1205
            ADD 1 TO ERRORS IN COMPT
            DISPLAY 'REC002: DUPLICATE LP KEY ' LP-ID WITH NO ADVANCING
            MOVE IN-READ OF COMPT TO IN-READ OF COMPT-P
            DISPLAY ' IN RECNO ' IN-READ OF COMPT-P.

PROCESS-SHOW-LP.
    PERFORM LOCATE-LP
    IF LP-OK
        ADD 1 TO LP-PROC IN COMPT
	GET LP-RECORD
        DISPLAY 'REC002: LP ID     : ' LP-ID
        DISPLAY '        LP NAME   : ' LP-NAME
        DISPLAY '        LP ARTIST : ' LP-AUTHOR
        DISPLAY 'REC002: ----------'.

PROCESS-LIST-LP.
    PERFORM PROCESS-SHOW-LP
    IF LP-OK
        MOVE SPACE TO EOS-SW
        FIND FIRST TRACK-RECORD RECORD OF LP-SET SET
        PERFORM LIST-TRACKS THRU END-LIST-TRACKS UNTIL EOS.

LIST-TRACKS.
    IF ERROR-COUNT NOT EQUAL ZERO
        IF ERROR-STATUS EQUALS 0307
            MOVE 'S' TO EOS-SW
            GO TO END-LIST-TRACKS
        ELSE
            DISPLAY 'REC002: UNEXPECTED DBMS ERROR'
            ADD 1 TO ERRORS OF COMPT
            GO TO END-LIST-TRACKS.
    GET TRACK-RECORD
    IF ERROR-COUNT NOT EQUAL ZERO
        DISPLAY 'REC002: UNEXPECTED DBMS ERROR'
        ADD 1 TO ERRORS OF COMPT
        GO TO END-LIST-TRACKS
    ELSE
	MOVE TRACK-NUMBER TO TLNUM
        MOVE TRACK-NAME TO TLNAME
        MOVE TRACK-SECONDS TO TLSECONDS
        DISPLAY TRACK-LINE.
    FIND NEXT TRACK-RECORD RECORD OF LP-SET SET.

END-LIST-TRACKS.

PROCESS-UPDATE-LP.
    PERFORM LOCATE-LP
    IF LP-OK
        GET LP-RECORD
        MOVE LPNAME   TO LP-NAME
        MOVE LPARTIST TO LP-AUTHOR
        MODIFY LP-RECORD
        IF ERROR-COUNT EQUAL ZERO
            DISPLAY 'REC002: RECORD ' LPID ' HAS BEEN UPDATED'
            ADD 1 TO LP-PROC OF COMPT.

PROCESS-DELETE-LP.
    PERFORM LOCATE-LP
    IF LP-OK
        DELETE LP-RECORD ONLY
        IF ERROR-COUNT EQUAL ZERO
            DISPLAY 'REC002: LP ' LPID ' AND ITS TRACKS HAS BEEN DELETED'
            ADD 1 TO LP-PROC OF COMPT
        ELSE
            ADD 1 TO ERRORS OF COMPT.


PROCESS-CREATE-TRACK.
    PERFORM LOCATE-LP
    IF LP-OK
        MOVE TRACKNUM TO TRACK-NUMBER
        MOVE TRACKNAME TO TRACK-NAME
        MOVE TRACKSECONDS TO TRACK-SECONDS
        STORE TRACK-RECORD
        IF ERROR-COUNT EQUAL ZERO
            DISPLAY 'REC002: ADDED TRACK WITH ID ' LPID '/' TRACK-NUMBER
            ADD 1 TO TRACK-PROC OF COMPT
        ELSE
            ADD 1 TO ERRORS OF COMPT
            MOVE IN-READ OF COMPT TO IN-READ OF COMPT-P
            DISPLAY 'REC002: DUPLICATE TRACK ' TRACK-NUMBER
                      WITH NO ADVANCING
            DISPLAY ' IN RECNO ' IN-READ OF COMPT-P.

PROCESS-SHOW-TRACK.
    PERFORM LOCATE-LP
    IF LP-OK
        PERFORM LOCATE-TRACK
        IF TRACK-OK
            DISPLAY 'REC002: LP ID         : ' LPID
            DISPLAY '        TRACK NUMBER  : ' TRACK-NUMBER
            DISPLAY '        TRACK NAME    : ' TRACK-NAME
            MOVE TRACK-SECONDS TO SECONDS-P
            DISPLAY '        TRACK SECONDS : ' SECONDS-P
            DISPLAY 'REC002: --------------'
        ELSE
            DISPLAY 'REC002: TRACK NOT FOUND: ' LPID '/' TRACKNUM
            ADD 1 TO ERRORS OF COMPT.

PROCESS-DELETE-TRACK.
    PERFORM LOCATE-LP
    IF LP-OK
        PERFORM LOCATE-TRACK
        IF NOT TRACK-OK
            DISPLAY 'REC002: TRACK ' LPID '/' TRACKNUM ' NOT FOUND'
            ADD 1 TO ERRORS OF COMPT
        ELSE
            DELETE TRACK-RECORD ONLY
            IF ERROR-COUNT EQUAL ZERO
                ADD 1 TO TRACK-PROC OF COMPT
                DISPLAY 'REC002: TRACK ' LPID '/' TRACK-NUMBER ' DELETED'.

PROCESS-UPDATE-TRACK.
    PERFORM LOCATE-LP
    IF LP-OK
        PERFORM LOCATE-TRACK
        IF NOT TRACK-OK
            DISPLAY 'REC002: TRACK ' LPID '/' TRACKNUM ' NOT FOUND'
            ADD 1 TO ERRORS OF COMPT
        ELSE
            GET TRACK-RECORD
            MOVE TRACKNAME TO TRACK-NAME
            MOVE TRACKSECONDS TO TRACK-SECONDS
            MODIFY TRACK-RECORD
            IF ERROR-COUNT EQUAL ZERO
                ADD 1 TO TRACK-PROC OF COMPT
                DISPLAY 'REC002: TRACK ' LPID '/' TRACK-NUMBER ' UPDATED'.

LOCATE-LP.
    MOVE SPACE TO LP-OK-SW
    MOVE LPID OF TRANS TO LP-ID
    FIND LP-RECORD RECORD
    IF ERROR-COUNT EQUAL ZERO
        MOVE 'S' TO LP-OK-SW
    ELSE
        ADD 1 TO ERRORS OF COMPT
        DISPLAY 'REC002: LP NOT FOUND ' LP-ID WITH NO ADVANCING
        MOVE IN-READ OF COMPT TO IN-READ OF COMPT-P
        DISPLAY ' IN RECNO ' IN-READ OF COMPT-P.

LOCATE-TRACK.
    PERFORM LOCATE-LP
    IF LP-OK
        MOVE SPACE TO TRACK-OK-SW EOS-SW
        FIND FIRST TRACK-RECORD RECORD OF LP-SET SET
        PERFORM SCAN-TRACKS THRU END-SCAN-TRACKS UNTIL EOS OR TRACK-OK.

SCAN-TRACKS.
    IF ERROR-COUNT NOT EQUAL ZERO
        IF ERROR-STATUS EQUALS 0307
            MOVE 'S' TO EOS-SW
            GO TO END-SCAN-TRACKS
        ELSE
            DISPLAY 'REC002: UNEXPECTED DBMS ERROR'
            ADD 1 TO ERRORS OF COMPT
            GO TO END-SCAN-TRACKS.
    GET TRACK-RECORD
    IF ERROR-COUNT NOT EQUAL ZERO
        DISPLAY 'REC002: UNEXPECTED DBMS ERROR'
        ADD 1 TO ERRORS OF COMPT
    ELSE
        IF TRACK-NUMBER EQUAL TRACKNUM
            MOVE 'S' TO TRACK-OK-SW
        ELSE
            FIND NEXT TRACK-RECORD RECORD OF LP-SET SET.

END-SCAN-TRACKS.

REPORT-ERROR.	
    DISPLAY 'REC002: WRONG TRANSACTION CODE: ' 
             TCODE OF TRANS WITH NO ADVANCING
    MOVE IN-READ OF COMPT TO IN-READ OF COMPT-P
    DISPLAY ' RECNO: ' IN-READ OF COMPT-P
    ADD 1 TO ERRORS OF COMPT
    .
   
DO-READ.
    READ RECA02
        AT END MOVE 'S' TO EOF-SW.
    IF NOT EOF ADD 1 TO IN-READ OF COMPT
    .

DO-STATS.
    MOVE CORR COMPT TO COMPT-P.
    DISPLAY 'REC002: RECORDS READ : ' IN-READ OF COMPT-P.
    DISPLAY 'REC002:           LP : ' LP-PROC OF COMPT-P.
    DISPLAY 'REC002:       TRACKS : ' TRACK-PROC OF COMPT-P.
    DISPLAY 'REC002:       ERRORS : ' ERRORS OF COMPT-P.
    DISPLAY 'REC002:      IGNORED : ' IGNORED OF COMPT-P.

FINISH.
    ENTER MACRO STATS
    CLOSE AREA LP-AREA
    CLOSE JOURNAL
    STOP RUN
    .
    